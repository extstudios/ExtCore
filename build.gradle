plugins {
    id 'java'
    id 'maven-publish'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'org.extstudios'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}


dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")

    implementation 'com.zaxxer:HikariCP:7.0.2'

    compileOnly 'com.google.code.gson:gson:2.13.2'

    compileOnly 'com.mysql:mysql-connector-j:9.5.0'
    compileOnly 'org.postgresql:postgresql:42.7.8'
    compileOnly 'org.xerial:sqlite-jdbc:3.51.1.0'

}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    withSourcesJar()
    withJavadocJar()

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')

    relocate 'com.zaxxer.hikari', 'org.extstudios.extcore.libs.hikari'

    dependencies {
        include(dependency('com.zaxxer:HikariCP'))
    }
}

tasks.build.dependsOn(shadowJar)

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.extstudios'
            artifactId = 'extcore'

            artifact(shadowJar) {
                classifier = null
            }

            artifact(sourcesJar)
            artifact(javadocJar)

            pom {
                name = 'ExtCore'
                description = 'Comprehensive Minecraft plugin library with Folia/Paper support'
                url = 'https://github.com/extstudios/ExtCore'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'eextr0'
                        name = 'eextr0'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/extstudios/ExtCore.git'
                    developerConnection = 'scm:git:ssh://github.com/extstudios/ExtCore.git'
                    url = 'https://github.com/extstudios/ExtCore'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }

    tasks.named('publishMavenJavaPublicationToMavenLocal') {
        dependsOn(tasks.named('jar'))
        dependsOn(tasks.named('shadowJar'))
        dependsOn(tasks.named('sourcesJar'))
        dependsOn(tasks.named('javadocJar'))
    }
}